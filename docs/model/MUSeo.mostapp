application MUSeo {
    vendor MU
    author "Michael Ueberschaer"
    email "kontakt@webdesign-in-bremen.com"
    url "http://webdesign-in-bremen.com"
    version "2.0.0"
    prefix museo
    generatorSettings {
        settingsContainer Settings
        targetCoreVersion ZK136
        generateAccountApi false
        generateSearchApi false
        generateMailzApi false
        generateListBlock false
        generateModerationBlock false
        generateListContentType false
        generateDetailContentType false
        generateNewsletterPlugin false
        generateModerationPanel false
        generatePendingContentSupport false
        generateExternalControllerAndFinder false
        generateScribitePlugins false
        generateTagSupport false
        generateAtomTemplates false
        generateCsvTemplates false
        generateXmlTemplates false
        generateJsonTemplates false
        generateKmlTemplates false
        generateIcsTemplates false
        generatePoweredByBacklinksIntoFooterTemplates false
        skipFiles "
            images/admin.png,
            CHANGELOG.md,
            README.md
        "
        markFiles "
            javascript/MUSeo.js,
            lib/MUSeo/Api/HandleModules.php,
            lib/MUSeo/Controller/Ajax.php,
            lib/MUSeo/Installer.php,
            lib/MUSeo/Listener/ModuleDispatch.php,
            lib/MUSeo/Listener/Theme.php
            lib/MUSeo/Util/Model.php,
            lib/MUSeo/Version.php,
            style/style.css,
            templates/admin/config.tpl,
            templates/extension/edit.tpl,
            templates/metatag/display.tpl,
            templates/metatag/edit.tpl,
            templates/metatag/view.tpl
        "
    }
    modelLayer {
        modelContainer Model {
            numExampleRows 0
            entities {
                entity metatag leading {
                    nameMultiple metatags
                    displayPattern "#titleOfEntity#"
                    fields {
                        string titleOfEntity {
                            documentation "Enter the title of this entity; so you can find it!"
                        },
                        string focusKeyword {
                            documentation "The main keyword or keyphrase that this post/page is about"
                            defaultValue ""
                            mandatory false
                        },
                        string title {
                            documentation "SEO title"
                            mandatory false
                        },
                        string description {
                            documentation "Meta description"
                            mandatory false
                        },
                        string keywords {
                            documentation "Comma separated list of meta keywords - for example: Zikula, Framework"
                            mandatory false
                        },
                        list robotsIndex {
                            documentation "Meta robots index"
                            mandatory false
                            items {
                                default listItem Default ( "" ),
                                listItem Index ( ^index ),
                                listItem NoIndex ( noindex )
                            }
                        },
                        list robotsFollow {
                            dbName ""
                            documentation "Meta robots follow"
                            mandatory false
                            items {
                                default listItem Default ( "" ),
                                listItem Follow ( follow ),
                                listItem Nofollow ( nofollow )
                            }
                        },
                        list robotsAdvanced multiple {
                            defaultValue "-"
                            mandatory false
                            items {
                                default listItem Default ( "-" ),
                                listItem None ( none ),
                                listItem "No ODP" ( noodp ),
                                listItem "No YDIR" ( noydir ),
                                listItem "No Image Index" ( noimageindex ),
                                listItem "No Archive" ( noarchive ),
                                listItem "No Snippet" ( nosnippet )
                            }
                        },
                        url canonicalUrl {
                            documentation "Canonical url"
                            defaultValue ""
                            mandatory false
                        },
                        url redirectUrl {
                            documentation "301 redirect url"
                            defaultValue ""
                            mandatory false
                        },
                        string facebookTitle {
                            documentation "Facebook title overriding the default value"
                            defaultValue ""
                            mandatory false
                        },
                        text facebookDescription {
                            documentation "Facebook description overriding the default value"
                            defaultValue ""
                            mandatory false
                        },
                        upload facebookImage {
                            documentation "Facebook image overriding the default value"
                            mandatory false
                        },
                        string twitterTitle {
                            dbName ""
                            documentation "Twitter title overriding the default value"
                            defaultValue ""
                            mandatory false
                        },
                        text twitterDescription {
                            documentation "Twitter description overriding the default value"
                            defaultValue ""
                            mandatory false
                        },
                        upload twitterImage {
                            documentation "Twitter image overriding the default value"
                            mandatory false
                        },
                        string googlePlusTitle {
                            documentation "Google+ title overriding the default value"
                            defaultValue ""
                            mandatory false
                        },
                        text googlePlusDescription {
                            documentation "Google+ description overriding the default value"
                            defaultValue ""
                            mandatory false
                        },
                        upload googlePlusImage {
                            documentation "Google+ image overriding the default value"
                            mandatory false
                        },
                        string whatsAppTitle {
                            documentation "WhatsApp title overriding the default value"
                            defaultValue ""
                            mandatory false
                        },
                        integer pageAnalysisScore {
                            defaultValue "0"
                            mandatory false
                        },
                        string theModule {
                            documentation "Select the module!"
                            length 50
                        },
                        string functionOfModule {
                            documentation "Enter the function of your selected module!"
                            length 50
                        },
                        string objectOfModule {
                            documentation "Enter the object of your selected module; if not set it will work for the main function!"
                            mandatory false
                            length 50
                        },
                        string nameOfIdentifier {
                            documentation "Enter the name for the single identifier! For example \'id\'!"
                            mandatory false
                            length 20
                        },
                        integer idOfObject {
                            documentation
                            "Enter the id of your selected module; only required if you have entered a function that calls a single entity!"
                            mandatory false
                        },
                        string stringOfObject {
                            documentation "Enter the string of an object! For example \'Zikula\'!"
                            mandatory false
                            length 50
                        },
                        string extraInfos {
                            documentation
                            "Enter other parameters and their values comma separated like parameter1=value1,parameter2=value!"
                            mandatory false
                        }
                    }
                },
                entity extension {
                    nameMultiple extensions
                    displayPattern "#name#"
                    fields {
                        string name unique,
                        string controllerForView {
                            documentation "Enter the controller for the view of objects; for example view."
                            mandatory false
                            length 50
                        },
                        string controllerForSingleObject {
                            documentation "Select the controller for single entity of this module!"
                            length 50
                        },
                        string parameterForObjects {
                            documentation "Enter the paremeter for objects; for example ot!"
                            defaultValue ""
                            mandatory false
                            length 50
                        },
                        string nameOfIdentifier {
                            documentation "Enter the name of the identifier for this module; for example \'id\'!"
                        },
                        string extraIdentifier {
                            documentation "Enter other additional identifiers comma separated!"
                            mandatory false
                        }
                    }
                }
            }
            variables {
                varContainer General {
                    documentation "Here you can define general settings."
                    vars {
                        textVar modules {
                            documentation "Enter the allowed modules comma separated without whitespace, for example: Content,Tags,News"
                            value News
                        },
                        textVar ^controllers {
                            documentation "Enter the allowed controllers comma separated without whitespace!"
                            value "main,view,display"
                        }
                    }
                },
                varContainer SEO {
                    documentation "Here you can adjust basic SEO-related options."
                    sortOrder 2
                    vars {
                        listVar robotsIndex {
                            documentation "Default meta robots index"
                            value ^index
                            items {
                                listVarItem default ^index,
                                listVarItem noindex
                            }
                        },
                        listVar robotsFollow {
                            documentation "Default meta robots follow"
                            value follow
                            items {
                                listVarItem default follow,
                                listVarItem nofollow
                            }
                        },
                        boolVar disableAdvancedMeta {
                            documentation "Allows to disable the advanced part of the SEO meta box."
                            value ^true
                        },
                        textVar alexaVerify {
                            documentation "Alexa Verification ID"
                            value ""
                        },
                        textVar googleVerify {
                            documentation "Google Webmaster Tools"
                            value ""
                        },
                        textVar msVerify {
                            documentation "Bing Webmaster Tools"
                            value ""
                        },
                        textVar pinterestVerify {
                            documentation Pinterest
                            value ""
                        },
                        textVar yandexVerify {
                            documentation "Yandex Webmaster Tools"
                            value ""
                        },
                        textVar forceTransport {
                            documentation "Force the canonical to either http or https, when your site runs under both."
                            value ^default
                            maxLength 10
                        },
                        boolVar noodp {
                            documentation "Add noodp meta robots tag sitewide"
                            value ^false
                        },
                        boolVar noydir {
                            documentation "Add noydir meta robots tag sitewide"
                            value ^false
                        }
                    }
                },
                varContainer SocialMedia {
                    sortOrder 5
                    vars {
                        textVar facebookAdmins {
                            documentation "List of user ids for Facebook administration"
                            value ""
                        },
                        textVar facebookApps {
                            documentation "List of linked Facebook apps id\'s => Facebook app display names"
                            value ""
                        },
                        textVar facebookConnectKey {
                            documentation "Facebook connect key"
                            value ""
                        },
                        boolVar openGraphEnabled {
                            documentation "Add Open Graph meta data to your site\'s head section."
                            value ^true
                        },
                        intVar facebookAdminApp {
                            documentation "App to use as Facebook admin"
                            value "0"
                        },
                        textVar facebookSite {
                            documentation "Facebook page url"
                            value ""
                        },
                        textVar openGraphFrontpageTitle {
                            documentation "Open Graph frontpage title"
                            value ""
                        },
                        textVar openGraphFrontpageDescription {
                            documentation "Open Graph frontpage description"
                            value ""
                        },
                        textVar openGraphFrontpageImage {
                            documentation "Open Graph frontpage image"
                            value ""
                        },
                        textVar openGraphDefaultImage {
                            documentation "OpenGraph default image url"
                            value ""
                        },
                        boolVar twitterEnabled {
                            documentation "Add Twitter card meta data"
                            value ^false
                        },
                        textVar twitterSiteUser {
                            documentation "Site Twitter username"
                            value ""
                        },
                        textVar twitterDefaultCardType {
                            documentation "The default card type to use"
                            value summary
                        },
                        boolVar googlePlusEnabled {
                            documentation "Add Google+ specific post meta data"
                            value ^false
                        },
                        textVar googlePlusPublisherPage {
                            documentation "Google publisher page"
                            value ""
                        }
                    }
                }
            }
        }
    }
    controllerLayer {
        controllerContainer Controller {
            modelContext ( "MUSeo.Model" )
            processViews "MUSeo.View"
            controllers {
                adminController Admin {
                    actions {
                        mainAction Main,
                        viewAction View,
                        displayAction Display,
                        editAction Edit,
                        deleteAction Delete
                    }
                },
                userController User {
                    actions {
                        mainAction Main
                    }
                },
                ajaxController Ajax {
                    actions {
                        mainAction Main
                    }
                },
                customController HandleModules {
                    actions {
                        customAction CheckModule,
                        customAction GetEntities
                    }
                }
            }
        }
    }
    viewLayer {
        viewContainer View {
            controller "MUSeo.Controller"
        }
    }
}